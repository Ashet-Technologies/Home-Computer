
dummy_serial := datetime('%Y%m%d/%H%M')

vendor_id := "13"

root_dir := parent_directory(parent_directory(justfile_directory()))

all: ps2-input fb-video

ps2-input: (compile-expansion "./PS2 Input" "1")
fb-video: (compile-expansion "./Framebuffer Video" "2")



compile-expansion folder pid:
    {{root_dir}}/Scripts/ExpCard.py \
        --output "{{folder}}/eeprom.json" \
        "{{vendor_id}}" "{{pid}}" "{{dummy_serial}}"

    [ -f "{{folder}}/firmware.propan" ] && propan \
        --output "{{folder}}/firmware.bin" \
        --format flat \
        "{{folder}}/firmware.propan" || true 

    [ -f "{{folder}}/firmware.propan" ] && ashet-exp encode \
        --output "{{folder}}/eeprom.bin" \
        --firmware "{{folder}}/firmware.bin" \
        "{{folder}}/eeprom.json" || true 
    [ -f "{{folder}}/firmware.propan" ] || ashet-exp encode \
        --output "{{folder}}/eeprom.bin" \
        "{{folder}}/eeprom.json" || true 

    [ -f "{{folder}}/eeprom.json" ]

    hexdump -C "{{folder}}/eeprom.bin" \
        | tee "{{folder}}/eeprom.hex"
